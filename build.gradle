import java.util.regex.Matcher
import java.util.regex.Pattern

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext{
    flavor =  getCurrentFlavor()
    if (flavor.isEmpty())
    {
        flavor = "dev"
    }
    config = getProps('config/' + flavor +'.configuration')
}

def getProps(path)
{
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}
def getCurrentFlavor()
{
    Gradle gradle = getGradle()
    def pattern = Pattern.compile("([A-Z][A-Za-z]+)(Release|Debug)")
    def flavor = ""
    gradle.getStartParameter().getTaskNames().any{name ->
        Matcher matcher = pattern.matcher(name)
        if(matcher.find())
        {
            flavor = matcher.group(1).toLowerCase()
            return true
        }
    }
    return flavor
}